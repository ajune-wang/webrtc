# Copyright (c) 2018 The WebRTC project authors. All Rights Reserved.
#
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file in the root of the source
# tree. An additional intellectual property rights grant can be found
# in the file PATENTS.  All contributing project authors may
# be found in the AUTHORS file in the root of the source tree.

import("../../webrtc.gni")

rtc_source_set("task_queue") {
  visibility = [ "*" ]
  public = [
    "queued_task.h",
    "task_queue_priority.h",
  ]
}

# TODO(bugs.webrtc.org/10191): Merge the target into task_queue target above
# when support for link-time injection is dropped.
rtc_source_set("task_queue_factory") {
  visibility = [ "*" ]
  public = [
    "task_queue_base.h",
    "task_queue_factory.h",
  ]
  sources = [
    "task_queue_base.cc",
  ]

  deps = [
    ":task_queue",
    "//third_party/abseil-cpp/absl/base:core_headers",
    "//third_party/abseil-cpp/absl/strings",
  ]
}

rtc_source_set("task_queue_test") {
  visibility = [ "*" ]
  testonly = true
  sources = [
    "task_queue_test.cc",
    "task_queue_test.h",
  ]
  deps = [
    ":task_queue",
    ":task_queue_factory",
    "../../rtc_base:rtc_event",
    "../../rtc_base:rtc_task_queue_api",
    "../../rtc_base:timeutils",
    "../../test:test_support",
    "//third_party/abseil-cpp/absl/memory",
    "//third_party/abseil-cpp/absl/strings",
  ]
}

rtc_source_set("default_task_queue_factory") {
  # TODO(bugs.webrtc.org/10191): Make public when implemented for all
  # supported platforms.
  visibility = [
    ":global_task_queue_factory",
    ":task_queue_default_unittests",
  ]
  sources = [
    "default_task_queue_factory.cc",
    "default_task_queue_factory.h",
  ]
  deps = [
    ":task_queue_factory",
    "../../rtc_base:checks",
  ]
}

if (rtc_include_tests) {
  rtc_source_set("task_queue_default_unittests") {
    testonly = true
    sources = [
      "default_task_queue_unittest.cc",
    ]
    deps = [
      ":default_task_queue_factory",
      ":task_queue_test",
      "../../test:test_support",
    ]
  }
}

rtc_source_set("global_task_queue_factory") {
  # TODO(danilchap): Remove this target when task queue factory propagated to
  # all components that create TaskQueues.
  visibility = [ "*" ]
  sources = [
    "global_task_queue_factory.cc",
    "global_task_queue_factory.h",
  ]
  deps = [
    ":default_task_queue_factory",
    ":task_queue_factory",
    "../../rtc_base:checks",
  ]
}
